#include <Arduino.h>
#include <WiFi.h>
#include <WiFiMulti.h>
#include <FirebaseESP32.h>
#include "HX711.h"
#include <OneWire.h>
#include <DallasTemperature.h>
#include <time.h>

// ================= WIFI / FIREBASE =================
WiFiMulti wifi;

#define WIFI_SSID_1 "Hằng"
#define WIFI_PASS_1 "12345678"
#define WIFI_SSID_2 "Hang"
#define WIFI_PASS_2 "0971397361"

#define FIREBASE_HOST "https://test-fea1d-default-rtdb.asia-southeast1.firebasedatabase.app"
#define FIREBASE_API_KEY "AIzaSyCRiWwj3IyWB0GNT7lw-zx2g3U5o5Cw8iY"
#define USER_EMAIL "vuthithuhang24cv@gmail.com"
#define USER_PASSWORD "lovecats21.00"

FirebaseData fbData;
FirebaseConfig fbConfig;
FirebaseAuth fbAuth;

// ================= LOAD CELL =================
#define DOUT 21
#define SCK 22
HX711 loadCell;
float scaleFactor   = 505.4633;
float currentWeight = 0;
float previousWeight = -1;

// ================= TDS / TURBIDITY =================
#define TDS_PIN 34
float waterQuality = 0;

#define TURBIDITY_PIN 32
int turbidity = 0;

// ================= NHIỆT ĐỘ =================
#define TEMP_SENSOR_PIN 18
OneWire oneWire(TEMP_SENSOR_PIN);
DallasTemperature sensors(&oneWire);
float temperature = 0;

// ================= ĐỘNG CƠ L298N =================
// Motor A: guồng
#define ENA 5
#define IN1 26
#define IN2 27

// Motor B: thức ăn
#define ENB 23
#define IN3 14
#define IN4 12

// ================= TRẠNG THÁI FEEDING =================
bool feedingGram      = false;
unsigned long gramStartTime = 0;
float gramTarget      = 0;
float gramStartWeight = 0;

bool feedingAuto      = false;
unsigned long autoFeedStart = 0;

bool manualRunning    = false;   // motor đang chạy trong mode manual?

// ================= FORMAT THỜI GIAN =================
String formatTime() {
  time_t now = time(nullptr);
  struct tm* t = localtime(&now);
  char buf[20];
  sprintf(buf, "%02d:%02d %02d/%02d/%04d",
          t->tm_hour, t->tm_min,
          t->tm_mday, t->tm_mon + 1,
          t->tm_year + 1900);
  return String(buf);
}

void logFeed(float grams, const String& mode) {
  if (!Firebase.ready()) return;

  if (Firebase.getInt(fbData, "/logs/counter")) {
    int c = fbData.intData();
    String path = "/logs/log" + String(c + 1);
    Firebase.setFloat (fbData, path + "/gram", grams);
    Firebase.setString(fbData, path + "/mode", mode);
    Firebase.setString(fbData, path + "/time", formatTime());
    Firebase.setInt   (fbData, "/logs/counter", c + 1);
  } else {
    Firebase.setInt(fbData, "/logs/counter", 1);
  }
}

// ================= MOTOR CONTROL (CHẮC CHẮN STOP/START) =================
void startGuong() {
  digitalWrite(ENA, HIGH);
  digitalWrite(IN1, HIGH);
  digitalWrite(IN2, LOW);
}

void stopGuong() {
  digitalWrite(ENA, LOW);
  digitalWrite(IN1, LOW);
  digitalWrite(IN2, LOW);
}

void startFeeder() {
  digitalWrite(ENB, HIGH);
  digitalWrite(IN3, HIGH);
  digitalWrite(IN4, LOW);
}

void stopFeeder() {
  digitalWrite(ENB, LOW);
  digitalWrite(IN3, LOW);
  digitalWrite(IN4, LOW);
}

// ================= ĐỌC CẢM BIẾN =================
void readSensors() {
  // TDS
  int adc = analogRead(TDS_PIN);
  if (adc < 10) waterQuality = 0;
  else {
    float v = adc * 3.3 / 4096.0;
    waterQuality = (133.42 * pow(v, 3) - 255.86 * pow(v, 2) + 857.39 * v) * 0.75;
  }
  Firebase.setFloat(fbData, "/aquarium/water_quality", waterQuality);

  // Độ đục
  turbidity = analogRead(TURBIDITY_PIN);
  Firebase.setInt(fbData, "/aquarium/ts300b", turbidity);

  // Nhiệt độ
  sensors.requestTemperatures();
  temperature = sensors.getTempCByIndex(0);
  Firebase.setFloat(fbData, "/aquarium/temperature", temperature);

  // Cân nặng
  if (loadCell.is_ready()) {
    currentWeight = loadCell.get_units(10);
    if (abs(currentWeight - previousWeight) > 0.5) {
      previousWeight = currentWeight;
      Firebase.setFloat(fbData, "/aquarium/weight", currentWeight);
    }
  }
}

// ================= GUỒNG =================
void updateGuongControl() {
  bool guong = false;

  if (Firebase.getBool(fbData, "/aquarium/control/guong")
      && fbData.dataAvailable()) {
    guong = fbData.boolData();
  }

  if (guong) startGuong();
  else stopGuong();
}

// ================= FEEDING: AUTO + GRAM =================
void updateFeedingMode() {
  String mode = "";
  if (Firebase.getString(fbData, "/aquarium/control/thucan/mode")
      && fbData.dataAvailable())
    mode = fbData.stringData();

  bool state = false;
  if (Firebase.getBool(fbData, "/aquarium/control/thucan/state")
      && fbData.dataAvailable())
    state = fbData.boolData();

  float target = 0;
  if (Firebase.getFloat(fbData, "/aquarium/control/thucan/target_gram")
      && fbData.dataAvailable())
    target = fbData.floatData();

  // ========== AUTO ==========
  if (mode == "auto") {
    time_t now = time(nullptr);
    struct tm* t = localtime(&now);
    int h = t->tm_hour;
    int m = t->tm_min;

    if (!feedingAuto && state &&
        ((h == 6 && m == 0) || (h == 17 && m == 0))) {

      Firebase.setFloat(fbData,
        "/aquarium/_temp_start_weight", currentWeight);

      startFeeder();
      feedingAuto = true;
      autoFeedStart = millis();
    }

    if (feedingAuto &&
        (!state || millis() - autoFeedStart > 20000)) {

      stopFeeder();
      feedingAuto = false;
      Firebase.setBool(fbData,
        "/aquarium/control/thucan/state", false);

      float before = 0;
      Firebase.getFloat(fbData,
        "/aquarium/_temp_start_weight");
      before = fbData.floatData();

      float delta = currentWeight - before;
      if (delta < 0) delta = 0;

      logFeed(delta, "auto");
    }
  } else {
    if (feedingAuto) {
      feedingAuto = false;
      stopFeeder();
      Firebase.setBool(fbData,
        "/aquarium/control/thucan/state", false);
    }
  }

  // ========== GRAM ==========
  if (mode == "gram") {
    if (!feedingGram && target > 0 && state) {
      feedingGram = true;
      gramTarget = target;
      gramStartWeight = currentWeight;
      gramStartTime = millis();
      startFeeder();
    }

    if (feedingGram) {
      float diff = currentWeight - gramStartWeight;

      bool cancelByUser =
        (!state || target <= 0);

      if (diff >= gramTarget ||
          millis() - gramStartTime > 30000 ||
          cancelByUser) {

        feedingGram = false;
        stopFeeder();

        Firebase.setFloat(fbData,
          "/aquarium/control/thucan/target_gram", 0);
        Firebase.setBool (fbData,
          "/aquarium/control/thucan/state", false);

        float delta = currentWeight - gramStartWeight;
        if (delta < 0) delta = 0;

        logFeed(delta, "gram");
      }
    }
  } else {
    if (feedingGram) {
      feedingGram = false;
      stopFeeder();
      Firebase.setBool(fbData,
        "/aquarium/control/thucan/state", false);
    }
  }
}

// ================= MANUAL =================
void updateManualFeeding() {

  String mode = "";
  if (Firebase.getString(fbData, "/aquarium/control/thucan/mode")
      && fbData.dataAvailable())
    mode = fbData.stringData();

  bool state = false;
  if (Firebase.getBool(fbData, "/aquarium/control/thucan/state")
      && fbData.dataAvailable())
    state = fbData.boolData();

  if (mode != "manual") {
    if (manualRunning) {
      stopFeeder();
      manualRunning = false;
    }
    return;
  }

  if (state && !manualRunning) {
    startFeeder();
    manualRunning = true;
  }

  if (!state && manualRunning) {
    stopFeeder();
    manualRunning = false;
    logFeed(0, "manual");
  }
}

// ================= SETUP =================
void setup() {
  Serial.begin(115200);
  delay(500);

  wifi.addAP(WIFI_SSID_1, WIFI_PASS_1);
  wifi.addAP(WIFI_SSID_2, WIFI_PASS_2);
  while (wifi.run() != WL_CONNECTED) delay(300);

  fbConfig.database_url = FIREBASE_HOST;
  fbConfig.api_key      = FIREBASE_API_KEY;
  fbAuth.user.email     = USER_EMAIL;
  fbAuth.user.password  = USER_PASSWORD;
  Firebase.begin(&fbConfig, &fbAuth);

  loadCell.begin(DOUT, SCK);
  loadCell.set_scale(scaleFactor);
  delay(1500);
  loadCell.tare();

  sensors.begin();
  configTime(7 * 3600, 0, "pool.ntp.org");
  while (!time(nullptr)) delay(500);

  pinMode(ENA, OUTPUT); pinMode(IN1, OUTPUT); pinMode(IN2, OUTPUT);
  pinMode(ENB, OUTPUT); pinMode(IN3, OUTPUT); pinMode(IN4, OUTPUT);

  stopGuong();
  stopFeeder();
}

// ================= LOOP =================
void loop() {
  static unsigned long last = 0;
  if (millis() - last < 500) return;   
  last = millis();

  readSensors();
  updateGuongControl();
  updateFeedingMode();
  updateManualFeeding();
}
